ext {
    // 所有项目模块
    allProjectList = Arrays.asList(
            project(":core"),
            project(":ext"),
            project(":common"),
            project("demo"),
            project("demo-api")
    ).toSet()
    allProjectList.addAll(project(":core").subprojects)
    allProjectList.addAll(project(":ext").subprojects)

    // 空父工程
    pomProjectList = Arrays.asList(
            project(":core"),
            project(":ext")
    )

    // 需要打包的模块
    packageProjcetList = Arrays.asList(
            project(":demo")
    )
}

// 构建配置
buildscript {
    repositories {
        maven {
            allowInsecureProtocol = true
            url MAVEN_REPO
        }
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:' + VERSION_SPRINGBOOT
        classpath 'io.spring.gradle:dependency-management-plugin:' + VERSION_SPRING_PLUGIN
        classpath 'io.freefair.gradle:lombok-plugin:' + VERSION_LOMBOK_PLUGIN
    }

}

// 所有项目配置
allprojects {
    //gradle插件
    apply plugin: 'org.springframework.boot'
    apply from: "${rootDir}/spring.gradle"
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.freefair.lombok'

    group = "org.quanta"
    version = PROJECT_VERSION

    repositories {
        //maven仓库
        mavenLocal()
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public' }
        maven { url 'https://maven.aliyun.com/nexus/content/repositories/google' }
        maven { url 'https://maven.aliyun.com/nexus/content/repositories/jcenter' }
        mavenCentral()
    }
    // 统一依赖版本管理
    dependencyManagement {
        // springboot 版本管理
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:' + VERSION_SPRINGBOOT
        }

        // 其他依赖版本管理
        dependencies {
            dependency 'cn.hutool:hutool-all:5.8.5'
            dependency 'org.redisson:redisson:3.21.1'
            dependency 'org.projectlombok:lombok:1.18.28'
            dependency 'com.baomidou:mybatis-plus-boot-starter:3.5.3.2'
            dependency 'mysql:mysql-connector-java:8.0.33'
            dependency 'io.springfox:springfox-boot-starter:3.0.0'
            dependency 'io.springfox:springfox-swagger-ui:3.0.0'
            dependency 'com.github.xiaoymin:knife4j-spring-boot-starter:3.0.3'
        }
    }
}

// 仅对有代码编写的模块引入构建插件
configure(allProjectList - pomProjectList) { project ->
    apply plugin: 'java-library'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    // 所有项目共用依赖
    dependencies {
        api 'org.projectlombok:lombok'
    }
}

// 设置gradle编码
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}